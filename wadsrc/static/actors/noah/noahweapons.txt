// Unfortunately it looks like none of the weapons have official names :(

actor HandFeed : WolfWeapon
{
	weapon.ammouse1 0
	weapon.selectionorder 3700
	attacksound "weapon/knife/attack"
	+WEAPON.NOALERT
	states
	{
		Ready:
			HAND A 1 A_WeaponReady
			loop
		Fire:
			HAND BC 3
			HAND D 3 A_CustomPunch(1, false, CPF_ALWAYSPLAYSOUND)
			HAND E 3
			// In Noah's Ark weapons automatically refired, but it took at least one tic
			HAND A 1
			goto Ready
	}
}

actor Slingshot : WolfWeapon
{
	weapon.ammotype1 "Feed"
	weapon.selectionorder 1900
	attacksound "weapon/pistol/attack"
	states
	{
		Ready:
			SLNG A 1 A_WeaponReady
			loop
		Fire:
			SLNG B 3
			SLNG C 3 A_GunAttack
			SLNG DE 3
			SLNG A 1
			goto Ready
	}
}

actor SuperSlingshot : WolfWeapon
{
	attacksound "weapon/machine/attack"
	inventory.pickupsound "weapon/machine/pickup"
	weapon.ammotype1 "Feed"
	weapon.selectionorder 1300
	+INVENTORY.ALWAYSPICKUP
	states
	{
		Spawn:
			MGUN A -1
			stop
		Ready:
			SSLG A 1 A_WeaponReady
			loop
		Fire:
			SSLG B 3
		Hold:
			SSLG C 3
			SSLG D 3 A_GunAttack
			SSLG E 3 A_ReFire
			goto Ready
	}
}

actor AutoSlingshot : WolfWeapon
{
	attacksound "weapon/gatling/attack"
	inventory.pickupsound "weapon/gatling/pickup"
	weapon.ammotype1 "Feed"
	weapon.selectionorder 700
	-WEAPON.NOGRIN
	+WEAPON.ALWAYSGRIN
	+INVENTORY.ALWAYSPICKUP
	states
	{
		Spawn:
			CGUN A -1
			stop
		Ready:
			ASLG A 1 A_WeaponReady
			loop
		Fire:
			ASLG B 3
		Hold:
			ASLG C 3
			ASLG D 3 A_GunAttack
			ASLG D 0 A_GunAttack
			ASLG E 3 A_ReFire
			goto Ready
	}
}

actor CoconutSlingshot : WolfWeapon
{
	weapon.ammotype1 "CoconutAmmo"
	+INVENTORY.ALWAYSPICKUP
	+WEAPON.NOAUTOFIRE
	states
	{
		Ready:
			CSLG A 1 A_WeaponReady
			loop
		Fire:
			CSLG BC 3.5
			CSLG D 3.5 // A_FireCustomMissile
			CSLG E 3.5 // A_FireCustomMissile
			goto Ready
	}
}

actor MelonSlingshot : WolfWeapon
{
	weapon.ammotype1 "MelonAmmo"
	+INVENTORY.ALWAYSPICKUP
	+WEAPON.NOAUTOFIRE
	states
	{
		Ready:
			MSLG A 1 A_WeaponReady
			loop
		Fire:
			MSLG BCD 3
			MSLG E 3 //A_FireCustomMissile
			goto Ready
	}
}
