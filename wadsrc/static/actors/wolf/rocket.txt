actor Rocket
{
	// Note: Wolfenstein 3D defines the projectile size twice.
	// When doing collisions against walls it uses a radius of 8
	// When doing collisions against the player the delta needs to be within 48
	// So what to do?  I say we just copy Doom's radius for now.
	radius 11
	speed 16
	damage (random[MissileDamage](30, 61)) // random(0, 31) + 30
	seesound "missile/fire"
	deathsound "missile/hit"
	PROJECTILE
	states
	{
		Spawn:
			ROCK A 1.5 bright A_SpawnItem("Smoke")
			loop
		Death:
			BOOM ABC 3 bright
			stop
	}
}
// So it may not be a Chemical Rocket, but it's more original than Cyan Rocket :P
actor ChemicalRocket : Rocket
{
	states
	{
		Spawn:
			CRCK A 1.5 bright A_SpawnItem("ChemicalSmoke")
			loop
		Death:
			CBOM ABC 3 bright
			stop
	}
}

actor Smoke
{
	states
	{
		Spawn:
			SMOK ABCD 1.5
			stop
	}
}
actor ChemicalSmoke : Smoke
{
	states
	{
		Spawn:
			CSMK ABCD 1.5
			stop
	}
}

actor HighRocket
{
	radius 11
	speed 16
	damage (random[MissileDamage](30, 61))
	seesound "knight/missile"
	deathsound "missile/hit"
	PROJECTILE
	states
	{
		Spawn:
			HRCK A 1.5 bright A_SpawnItem("HighSmoke")
			loop
		Death:
			HBOM ABC 3 bright
			stop
	}
}
actor HighChemicalRocket : HighRocket
{
	states
	{
		Spawn:
			CHRK A 1.5 bright A_SpawnItem("HighChemicalSmoke")
			loop
		Death:
			CHBM ABC 3 bright
			stop
	}
}

actor HighSmoke
{
	states
	{
		Spawn:
			HSMK ABCD 3
			stop
	}
}
actor HighChemicalSmoke : HighSmoke
{
	states
	{
		Spawn:
			CHSM ABCD 3
			stop
	}
}
